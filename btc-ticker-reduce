#!/usr/bin/awk -f
#
# btc-ticker-reduce
# Public Domain.
#
# Usage: nc api.bitcoincharts.com 27007 | \
#	btc-ticker-reduce -v halflife=360 -v currency=USD

BEGIN {
	running_volume = 0
	running_total = 0
	time_previous = 0
	if (!halflife) {
		halflife = 3600
	}
	if (!currency) {
		currency = "USD"
	}
}

$8 ~ currency {
	volume = $2
	price = $6
	time_current = $4

	total = price * volume
	time_diff = time_current - time_previous
	time_previous = time_current
	halflives = time_diff/halflife

	if (halflives == 0) {
		backoff = 1
	} else if (halflives > 1074) {
		backoff = 0
	} else if (halflives < -1023) {
		backoff = 1e15
	} else {
		backoff = 2 ^ -halflives
	}
	if (backoff < 1e15) {
		running_total = (running_total * backoff) + total
		running_volume = (running_volume * backoff) + volume
	}

	running_price = running_total/running_volume

	printf "%.0f\n", running_price
	fflush()
}

