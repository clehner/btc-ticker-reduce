#!/usr/bin/awk -f
#
# btc-ticker-reduce
# Public Domain.
#
# Usage: nc api.bitcoincharts.com 27007 | \
#	btc-ticker-reduce -v halflife=360 -v currency=USD -v format='$%.2f'

BEGIN {
	running_volume = 0
	running_total = 0
	time_previous = 0
	catchup = 0
	if (!halflife) {
		halflife = 3600
	}
	if (!currency) {
		currency = "USD"
	}
	if (!format) {
		format = "%f"
	}
}

$8 ~ currency {
	volume = $2
	price = $6
	time_current = $4

	total = price * volume
	time_diff = time_current - time_previous
	time_previous = time_current
	halflives = time_diff/halflife

	if (catchup > 0) {
		catchup -= halflives
		if (catchup < 4) {
			# resume threshold
			halflives = catchup
		} else {
			# placeholder
			halflives = 0
		}
	}

	if (halflives == 0) {
		backoff = 1
	} else if (halflives > 1074) {
		# this trade was far later than the previous trade,
		# so discard previous running total/volume
		backoff = 0
	} else if (halflives < -32) {
		# this trade was too far in the past to affect the current price
		backoff = 1e15
		catchup = -halflives
	} else {
		backoff = 2 ^ -halflives
	}
	if (backoff < 1e15) {
		running_total = (running_total * backoff) + total
		running_volume = (running_volume * backoff) + volume
	}

	running_price = running_total/running_volume

	printf format "\n", running_price
	fflush()
}

